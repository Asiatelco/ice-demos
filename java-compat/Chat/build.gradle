buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "net.sf.proguard:proguard-gradle:6.0.2"
    }
}

slice {
    java {
        include = ["${projectDir}"]
        srcDir = '.'
        include = ["${projectDir}"]
    }
}

dependencies {
    compile 'com.jgoodies:jgoodies-common:1.8.0'
    compile 'com.jgoodies:jgoodies-looks:2.6.0'
    compile 'com.jgoodies:jgoodies-forms:1.8.0'
    compile localDependency("ice-compat")
    compile localDependency("glacier2-compat")
}

jar {
    archiveName = "ChatGUITEMP.jar"
    manifest {
        attributes 'Main-Class': "ChatDemoGUI.MainView"
    }
}

def javaHome = System.properties['java.home']

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    injars configurations.compile.resolve(), filter: '!META-INF/**'
    injars "${buildDir}/libs/ChatGUITEMP.jar"
    if(JavaVersion.current() <= JavaVersion.VERSION_1_8) {
        libraryjars "${javaHome}/lib/rt.jar"
        libraryjars "${javaHome}/lib/jsse.jar"
    } else {
        libraryjars "${javaHome}/jmods/java.base.jmod"
        libraryjars "${javaHome}/jmods/java.logging.jmod"
        libraryjars "${javaHome}/jmods/java.desktop.jmod"
        libraryjars "${javaHome}/jmods/java.prefs.jmod"
    }
    outjars "ChatGUI.jar"
    configuration "chatdemogui.pro"
}
assemble.dependsOn(proguard)

clean {
    delete("ChatGUI.jar")
}
