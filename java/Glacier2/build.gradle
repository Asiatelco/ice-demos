// **********************************************************************
//
// Copyright (c) 2003-2017 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

['callback', 'simpleChat', 'voip'].each {
    def name = it
    project(":demoGlacier2${it.capitalize()}") {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            compile localDependency("ice")
            compile localDependency("glacier2")
            if (!useLocalOnly) {
                runtime "org.apache.commons:commons-compress:1.14"
            }
        }

        // Don't create the default jar.
        jar.enabled = false

        // The voip demo has no clientJar.
        if(name != "voip") {
            demoJar("client")
        }

        // The simpleChat demo has no serverJar.
        if(name != "simpleChat") {
            demoJar("server")
        }
    }
}
