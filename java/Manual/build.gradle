// **********************************************************************
//
// Copyright (c) 2003-2017 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

['evictor_filesystem', 'lifecycle', 'printer', 'simple_filesystem'].each {
    // abc_def -> AbcDef
    def name = it.split('_').collect { it.capitalize() }.join()
    project(":demoManual${name}") {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            compile localDependency("ice")
            if (!useLocalOnly) {
                runtime "org.apache.commons:commons-compress:1.14"
            }
        }

        if(name == "EvictorFilesystem") {
            dependencies {
                compile localDependency("freeze")
            }
            dependencies {
                runtime files(dbJar)
            }
        }


        // Don't create the default jar.
        jar.enabled = false

        clean {
            delete fileTree(dir: 'db', excludes:['.gitignore', 'DB_CONFIG'])
        }

        demoJar("client")
        demoJar("server")
    }
}
